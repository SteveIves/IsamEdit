;;******************************************************************************
;;
;; Class:		SynPsg.Rps.RpsFieldCollection
;;
;; Description: Represents a collection of fields for a Repository structure
;;
;; Author:		Steve Ives, Synergex Professional Services Group
;;
;; Date: 		19th October 2007
;;
;;******************************************************************************
;; Copyright (c) 2007, Steve Ives
;; All rights reserved.
;;
;; Redistribution and use in source and binary forms, with or without
;; modification, are permitted provided that the following conditions are met:
;;
;; * Redistributions of source code must retain the above copyright notice, this
;;   list of conditions and the following disclaimer.
;;
;; * Redistributions in binary form must reproduce the above copyright notice,
;;   this list of conditions and the following disclaimer in the documentation
;;   and/or other materials provided with the distribution.
;;
;; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
;; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
;; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
;; DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
;; FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
;; DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
;; SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
;; CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
;; OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
;; OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
;;
;;******************************************************************************
;;

.define DDINFO_DEFINES_ONLY
.include "RPSLIB:ddinfo.def"
.undefine DDINFO_DEFINES_ONLY

import SynPsg.Rps
import System.Collections.Generic

namespace SynPsg.Rps

	public class RpsFieldCollection extends List<RpsField>
		
		; Constructor (don't load any fields)
		public method RpsFieldCollection
			endparams
			parent()
		proc
		endmethod

		; Constructor (load fields for a structure)
		public method RpsFieldCollection
			required in StructureName, a
			endparams
			parent()
		proc
			Repository.CheckIsOpen()
			LoadFields(StructureName)
		endmethod
		
		; Load structure fields
		private method LoadFields, void
			required in StructureName, a
			endparams
			record local_data
				MemHandle, int
				ix, int
			endrecord
		proc
			init local_data
			try 
			begin
				data p_s_info, s_info
				;Set the current structure and get structure information
				dd_struct(Repository.RpsControl,DDS_INFO,StructureName,p_s_info)
				if (Repository.RpsControl.error)
					throw new RpsFieldException("Failed to get information for repository structure "+StructureName)
				;Do we have any fields to load?
				if (p_s_info.si_childct) 
				begin
					;Size the field names array and get field names
					MemHandle = %mem_proc(DM_ALLOC,^size(strIdentifier)*p_s_info.si_childct)
					dd_field(Repository.RpsControl,DDF_SLIST,p_s_info.si_childct,^m(strIdentifier,MemHandle))
					if (Repository.RpsControl.error)
						throw new RpsFieldException("Failed to get field names for repository structure "+StructureName)
					;Add fields to the collection
					for ix from 1 thru p_s_info.si_childct
						this.Add(new RpsField(StructureName,^m(strIdentifier[ix],MemHandle)))
				end
			end
			finally
			begin
				;Free field names array
				if (MemHandle)
                    MemHandle = %mem_proc(DM_FREE,MemHandle)
			end
			endtry
		endmethod
		
		; Remove overlay fields from the collection
		public method RemoveOverlays, void
			record 
				index, int, 0
			endrecord
		proc
			repeat
			begin
				if (index<=(this.Count - 1)) then
				begin
					if (this[index].OverlaysField) then
						this.RemoveAt(index)
					else
						index+=1
				end
				else
					exitloop
			end
		endmethod

		; Remove fields marked as "Excluded by Language" from the collection
		public method RemoveExcludedByLanguage, void
			record 
				index, int, 0
			endrecord
		proc
			repeat
			begin
				if (index<=(this.Count - 1)) then
				begin
					if (this[index].ExcludedByLanguage) then
						this.RemoveAt(index)
					else
						index+=1
				end
				else
					exitloop
			end
		endmethod

		; Remove fields marked as "Excluded by Toolkit" from the collection
		public method RemoveExcludedByToolkit, void
			record 
				index, int, 0
			endrecord
		proc
			repeat
			begin
				if (index<=(this.Count - 1)) then
				begin
					if (this[index].ExcludedByToolkit) then
						this.RemoveAt(index)
					else
						index+=1
				end
				else
					exitloop
			end
		endmethod

		; Remove fields marked as "Excluded by ReportWriter" from the collection
		public method RemoveExcludedByReportWriter, void
			record 
				index, int, 0
			endrecord
		proc
			repeat
			begin
				if (index<=(this.Count - 1)) then
				begin
					if (this[index].ExcludedByReportWriter) then
						this.RemoveAt(index)
					else
						index+=1
				end
				else
					exitloop
			end
		endmethod

		; Remove fields marked as "Excluded by Web" from the collection
		public method RemoveExcludedByWeb, void
			record 
				index, int, 0
			endrecord
		proc
			repeat
			begin
				if (index<=(this.Count - 1)) then
				begin
					if (this[index].ExcludedByWeb) then
						this.RemoveAt(index)
					else
						index+=1
				end
				else
					exitloop
			end
		endmethod

	endclass

endnamespace
